#-------------------------------------------------------------------------------
# pbpk_simple.model
#
# A simple PBPK model that describes absorption, distribution, metabolism, and
# excretion of Substance X.
#
# Author: Dustin Kapraun, U.S. EPA, June 2022
#-------------------------------------------------------------------------------

States = {
    A_G,            # Amount in gut lumen (mg)
    A_L,            # ... liver (mg)
    A_F,            # ... fat (mg)
    A_AB,           # ... arterial blood (mg)
    A_VB,           # ... venous blood (mg)
    A_R,            # ... rest of body (mg)
    A_dose,         # Cumulative amount that has entered organism (mg)
    A_m,            # Cumulative amount that has been metabolized (mg)
    AUC,            # Area under the venous blood concentration curve (mg*h/L)
};

Outputs = {
    A_tot,          # Total amount (mg)
    C_L,            # Concentration in liver (mg/L)
    C_F,            # ... fat (mg/L)
    C_AB,           # ... arterial blood (mg/L)
    C_VB,           # ... venous blood (mg/L)
    C_R,            # ... rest of body (mg/L)
    R_m,            # Rate of metabolism (mg/h)
    R_abs,          # Rate of absorption (mg/h)
};

Inputs = {};

M = 0.25;
Q_CC = 15.0;
Q_LC = 0.21;
Q_FC = 0.06;
V_LC = 0.04;
V_FC = 0.07;
V_ABC = 0.02;
V_VBC = 0.05;
 
P_L = 3.5;
P_F = 86.5;
P_R = 2.3;
V_maxC = 10.0;
K_m = 6.0;
k_abs = 1.25;

D_oral = 0.0;
D_IV = 0.0;

Q_C = 0.0;
Q_L = 0.0;
Q_F = 0.0;
Q_RC = 0.0;
Q_R = 0.0;
V_L = 0.0;
V_F = 0.0;
V_AB = 0.0;
V_VB = 0.0;
V_RC = 0.0;
V_R = 0.0;
V_max = 0.0;
R_oral = 0.0;
R_IV = 0.0;

Initialize {
    A_G = 0.0;
    A_L = 0.0;
    A_F = 0.0;
    A_AB = 0.0;
    A_VB = 0.0;
    A_R = 0.0;
    A_dose = 0.0;
    A_m = 0.0;
    AUC = 0.0;

    Q_C = Q_CC * pow(M, 0.75);
    Q_L = Q_LC * Q_C;
    Q_F = Q_FC * Q_C;
    Q_RC = 1.0 - (Q_LC + Q_FC);
    Q_R = Q_RC * Q_C;
    V_L = V_LC * M;
    V_F = V_FC * M;
    V_AB = V_ABC * M;
    V_VB = V_VBC * M;
    V_RC = 1.0 - (V_LC + V_FC + V_ABC + V_VBC);
    V_R = V_RC * M;
    V_max = V_maxC * pow(M, 0.75);
    R_oral = D_oral * M / 24;
    R_IV = D_IV * M / 24;
}

Dynamics {
    A_tot = A_dose - (A_G + A_L + A_F + A_AB + A_VB + A_R) - A_m;
    C_L = A_L / V_L;
    C_F = A_F / V_F;
    C_AB = A_AB / V_AB;
    C_VB = A_VB / V_VB;
    C_R = A_R / V_R;
    R_m = (V_max * C_L / P_L) / (K_m + C_L / P_L);
    R_abs = k_abs * A_G;

    dt(A_G) = R_oral - R_abs;
    dt(A_L) = Q_L * (C_AB - C_L / P_L) + R_abs - R_m;
    dt(A_F) = Q_F * (C_AB - C_F / P_F);
    dt(A_AB) = Q_C * (C_VB - C_AB);
    dt(A_VB) = Q_L * C_L / P_L + Q_F * C_F / P_F + Q_R * C_R / P_R - Q_C * C_VB + R_IV;
    dt(A_R) = Q_R * (C_AB - C_R / P_R);
    dt(A_dose) = R_oral + R_IV;
    dt(A_m) = R_m;
	dt(AUC) = C_VB;
}

End.
